apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.hugo'
apply plugin: 'maven'

android {
    def keystorePropertiesFile = rootProject.file('keystore.properties')
    def keystoreProperties = new Properties()
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
    // 此部分可以通过project structure -> singing 设置，之后需要在build type中选择相应的sigingconfig才可以打release包
    // 如果未设置，利用generate sign apk打的包时无法安装的。
    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
        debug {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    compileSdkVersion 25
    buildToolsVersion '25.0.3'
    useLibrary 'org.apache.http.legacy'
    defaultConfig {
        applicationId "com.study.shenxing.caesar"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", "JOKE_URL", "\"http://www.baidu.com\""
            buildConfigField "boolean", "LOG_SWITCH", "false"
            signingConfig signingConfigs.release
            applicationIdSuffix '.release'
        }
        debug {
            buildConfigField "String", "JOKE_URL", "\"http://www.google.com\""
            buildConfigField "boolean", "LOG_SWITCH", "true"
            signingConfig signingConfigs.debug
            applicationIdSuffix '.debug'
        }
    }
    lintOptions {
        abortOnError false
    }
    defaultConfig {
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

//        ndk {
//            moduleName 'app'
//            abiFilters 'armeabi', 'armeabi-v7a', 'x86'
//            ldLibs 'log', 'z', 'm'
//        }
    }
//    sourceSets {
//        main {
//            jniLibs.srcDirs = ['libs']
//        }
//    }
    uploadArchives {
        // 基于maven
        repositories.mavenDeployer {
            // 配置上传的url
            repository(url: "http://127.0.0.1:8081/repository/com.caesar/") {
                authentication(userName: "shenxing", password: "shenxing583")
            }
            pom.version = "0.0.1"
            pom.artifactId = "CaesarDemo"
            pom.groupId = "com.caesar.shenxing"
        }
    }
    // 为了解决版本不一致问题，似乎是加入react native代码引起的
    // 参考 https://stackoverflow.com/questions/37347326/errorconflict-with-dependency-com-google-code-findbugsjsr305/37357786
    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:1.3.9'
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def outputFile = output.outputFile;
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                def dirname = outputFile.parent;
                def filename = "shenxing_test_app-${defaultConfig.versionName}-${variant.flavorName}.apk";
                output.outputFile = new File(dirname, filename);
            }
        }
    }

}


dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    // Required -- JUnit 4 framework
    // Optional -- Mockito framework
    // Set this dependency to use JUnit 4 rules
    // Set this dependency to build and run Espresso tests
    // Set this dependency to build and run UI Automator tests
    // another dependencies of espresso
    // vectorDrawable必须
    // From node_modules.
    androidTestCompile 'com.android.support:support-annotations:25.2.0'
    compile 'com.facebook.react:react-native:+'
    compile 'com.android.support:appcompat-v7:25.2.0'
    compile 'com.android.support:design:25.2.0'
    compile 'com.android.support:recyclerview-v7:25.2.0'
    compile 'com.android.support:support-v4:25.2.0'
    compile 'org.greenrobot:eventbus:3.0.0'
    compile 'com.loopj.android:android-async-http:1.4.9'
    compile 'com.jakewharton.scalpel:scalpel:1.1.2'
    compile 'com.android.support.constraint:constraint-layout:1.0.0-alpha7'
    compile 'com.github.Aspsine:SwipeToLoadLayout:1.0.4'
    compile 'com.airbnb.android:lottie:1.5.3'
    compile 'com.facebook.rebound:rebound:0.3.8'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
    androidTestCompile 'com.android.support:support-annotations:25.2.0'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
    compile 'com.android.volley:volley:1.0.0'
    compile 'com.squareup.okhttp3:okhttp:3.9.0'
    compile 'com.alibaba:fastjson:1.1.64.android'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'io.reactivex:rxjava:1.3.4'
}
